/*
 * Copyright (c) 2015
 * Ministério do Planejamento, Orçamento e Gestão
 * República Federativa do Brasil
 *
 * Este programa é um software livre; você pode redistribuí-lo
 * e/ou modificá-lo sob os termos da Licença de Software SIOP; O
 * texto da licença pode ser consultado no arquivo anexo a esta
 * distribuição ou em <http://www.siop.gov.br/licença>.
 */

apply plugin: 'groovy'
apply plugin: 'maven-publish'

apply from: 'gradle/idea.gradle'
apply from: 'gradle/asciidoclet.gradle'

group = 'br.gov.siop.gradle'
archivesBaseName = 'thrift-gradle-plugin'
version = '0.1.0'
description = 'A gradle plugin for compiling Thrift IDL files using the scrooge or thrift compilers'

ext {
    title = 'Thrift Gradle Plugin'
    organizationId = 'br.gov.siop'
    organizationName = 'SIOP'
    organizationUrl = 'http://oss.siop.gov.br/'

    licenseName = 'Licença de Software SIOP'
    licenseUrl = 'http://www.siop.gov.br/licença'

    createdBy = "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')})"
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library',          version: '2.10.+'
    compile group: 'com.twitter',    name: 'scrooge-generator_2.10', version: '3.17.0'

    compile gradleApi()
    compile localGroovy()
}

processResources {
    from(['LICENSE', 'NOTICE']) {
        into 'META-INF'
    }
}

compileGroovy {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Specification-Title': title,
                'Specification-Version': version,
                'Specification-Vendor': organizationName,
                'Implementation-Title': title,
                'Implementation-Version': version,
                'Implementation-Vendor': organizationName,
                'Implementation-Vendor-Id': organizationId,
                'Implementation-Vendor-URL': organizationUrl,
                'Created-By': createdBy,
        )
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId archivesBaseName

            from components.java

            pom.withXml {
                def projectNode = asNode()
                projectNode.appendNode('name', title)
                projectNode.appendNode('description', description)
                projectNode.appendNode('url', organizationUrl)

                def organizationNode = projectNode.appendNode('organization')
                organizationNode.appendNode('name', organizationName)
                organizationNode.appendNode('url', organizationUrl)

                def licenseNode = projectNode.appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', licenseName)
                licenseNode.appendNode('url', licenseUrl)
                licenseNode.appendNode('distribution', 'repo')
            }

            artifact sourcesJar
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}